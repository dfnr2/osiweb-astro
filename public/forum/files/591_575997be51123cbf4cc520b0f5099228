 0 REM LISTING OF PROGRAM ADVENT
 5 CLEAR:FORI=1TO12:PRINT:NEXT:PRINTTAB(25);"*** ADVENTURE ***"
 6 FORI=1TO10:PRINT:NEXT:RESTORE
 10 DIMMZ(200),DR(200),MPG(20),PP(20),THG(17)
 13 THG$="KEYS LANTERN FOOD BOTTLE WATER SWORD TREASURE WAND ROPE "
 14 THG$=THG$+"DIAMONDS AXE OIL CROWN EMERALD SILVER "
 15 MG(1)=16:MG(2)=33:YA$="YOU ARE AT ":YI$="YOU ARE IN "
 16 MG(0)=MG(2):BK$="A BLACK KNIGHT ":RV$=CHR$(27)+CHR$(106)
 17 THG(1)=1:THG(2)=6:THG(3)=14:THG(4)=19:THG(5)=26:THG(6)=32:THG(7)=38
 18 THG(8)=47:THG(9)=52:THG(10)=57:THG(11)=66:THG(12)=70:THG(13)=74
 19 THG(14)=80:THG(15)=88:THG(16)=95:PV$=CHR$(27)+CHR$(107)
 20 FORI=1TO20:PP(I)=0:NEXT
 25 INPUT"ARE YOU A BOY OR A GIRL";A$
 30 IFA$="BOY"ORA$="GIRL"THEN35
 32 PRINT"PLEASE ANSWER BOY OR GIRL!":FORI=1TO500:NEXT:GOTO25
 35 DEFFNR(Q)=INT(Q*RND(1))
 36 DEFFNO(X)=INT(DR(X)-100*INT(DR(X)/100))
 40 P$="PRINCESS":IFA$="GIRL"THENP$="PRINCE"
 42 MW$="XYZZY":IFFNR(99)>66THENMW$="CIGAM"
 45 IFFNR(99)>66THENMW$="RENOB"
 100 FORI=1TO100:READMZ(I):MZ(I+100)=-1:NEXT:MZ(0)=0
 110 FORI=1TO100:READDR(I):DR(I+100)=-1:NEXT:DR(0)=0
 120 FORI=0TO20:READMPG(I):NEXT
 135 FORI=1TO12:PRINT:NEXT:PRINTTAB(25);"*** ADVENTURE ***":PRINT
 136 PRINT:GOTO500
 140 DATA9,3,1,3,3,6,6,2,6,6,3,6,2,2,3,7,2,2,6,3,3,4,4,4,11,9,17,12
 145 DATA12,17,19,16,20,18,21,12,17,22,23,26,8,21,12,14,24,12,16,17
 150 DATA12,23,9,13,13,13,13,13,25,26,5,9,26,26,27,12,12,26,26,26
 155 DATA26,26,26,26,26,26,26,16,17,12,12,19,12,21,19,14,16,17,28,12
 160 DATA12,16,29,30,29,13,34,31,36,32,33,35
 162 DATA9909,9903,9999,9911,9918,9915,9915,1515,9915,9915,9917,9915
 164 DATA9915,9916,9919,509,9915,9915,9915,9917,9908,9908,9917,9917
 166 DATA9917,9909,9918,9908,9908,9918,9910,9914,9919,9909,9915,9908
 168 DATA9918,9909,9999,9913,10,9915,9908,9907,9915,9908,9914,9917
 170 DATA9908,9999,9909,9908,9908,9908,9908,9918,9999,9910,9900,9909
 172 DATA9915,9919,9918,9908,9908,9915,9919,9919,9915,9915,9912,9914
 174 DATA9917,9917,9917,9917,9918,9908,9908,9910,9908,9915,9910,9907
 176 DATA9913,9917,9917,9908,9908,9914,9903,9901,9911,9908,9908,9917
 178 DATA9917,9917,9904,9905
 180 DATA0,3,3,3,3,59,0,33,28,45,49,0,69,95,0,44,0,0,0,0,0
 200 IFPIR<>0THEN299
 201 IFPOZ=3THEN299
 202 IFSHDW<>0THEN210
 204 IFFNR(99)<96THEN299
 206 SHDW=99
 210 PRINT:PRINT"A SHADOWY FIGURE IS FOLLOWING YOU!":PRINT
 212 FORI=16TO6STEP-1:IFPP(I)<>0ANDSW>FNR(10)+5THEN215
 213 NEXT:SW=SW+1:GOTO299
 215 PRINT"THE SHADOW LEAPS OUT OF THE DARKNESS."
 216 PRINT"`HAR HAR`, CRIES A BEARDED PIRATE, "
 220 PRINT"`ME THINKS ME WILL TAKE A LITTLE OF YUR BOOTY!":PRINT
 222 IFI=16THENB$=P$:GOTO227
 225 B$=MID$(THG$,THG(I),THG(I+1)-1-THG(I))
 227 PRINT"THAT ";B$;" WILL DO JUST FINE!`"
 228 PRINT:PRINT"WITH THAT HE SCURRIES OFF TO HIS TREASURE CAVE!"
 230 PRINT:PRINT:PP(I)=0:MPG(I)=33
 240 PIR=99
 250 PC=PC-1
 299 RETURN
 500 POZ=8:GOTO10000
 600 TEST=0:FORI=1TO15:IFMPG(I)<>POZTHEN610
 605 IFPP(I)<>0ORB$<>MID$(THG$,THG(I),THG(I+1)-1-THG(I))THEN610
 607 GOTO620
 610 NEXT:RETURN
 620 TEST=99:RETURN
 650 TEST=0:FORI=1TO15
 655 IFB$=MID$(THG$,THG(I),THG(I+1)-1-THG(I))ANDPP(I)<>0THEN620
 658 NEXT:RETURN
 660 TEST=99:RETURN
 700 IFD1<>0THEN710
 702 PRINT"AN ANGRY DWARF CAME IN, THREW A LITTLE AXE, WHICH MISSED, "
 703 PRINT"CURSED, AND RAN AWAY!":PRINT:PRINT
 705 MPG(11)=POZ:D1=99:PRINT:GOTO10010
 710 IFD1<>0THENDWF=DWF+2:DN=99
 715 PRINT"THERE`S AN ANGRY DWARF IN THE ROOM WITH YOU!"
 717 PRINT"HE THROWS A SINGLE KNIFE!";
 720 IFFNR(DWF)<=5THENPRINT"  IT MISSES!":PRINT:GOTO10010
 750 PRINT"  IT HITS YOU!":KILL=KILL+1:IFKILL>=2THEN14010
 760 PRINT:GOTO11001
 800 PRINT:IFMPG(1)=POZTHENPRINT"THERE`S A SET OF KEYS HERE!"
 802 IFMPG(2)=POZTHENPRINT"A LANTERN RESTS HERE!"
 804 IFMPG(3)=POZTHENPRINT"THERE IS TASTY FOOD HERE!"
 805 IFMPG(4)=POZTHENPRINT"A SMALL BOTTLE IS LAYING HERE!"
 806 IFMPG(5)=POZTHENPRINT"THERE IS MUCH WATER HERE!"
 808 IFMPG(6)=POZTHENPRINT"A GOLDEN SWORD LIES HERE!"
 809 IFMPG(7)=POZTHENPRINT"THERE`S A PIRATE`S TREASURE CHEST HERE!"
 810 IFMPG(8)=POZTHENPRINT"A MAGIC WAND FLOATS IN MID AIR!"
 815 IFMPG(9)=POZTHENPRINT"A ROPE OF GOLDEN THREAD IS COILED HERE!"
 820 IFMPG(10)=POZTHENPRINT"MANY DIAMONDS ARE SCATTERED HERE!"
 825 IFMPG(11)=POZANDNA=0THENPRINT"A LITTLE AXE IS HERE!"
 830 IFMPG(12)=POZTHENPRINT"A SMALL POOL OF OIL IS HERE!"
 835 IFMPG(13)=POZTHENPRINT"THERE`S A KING`S CROWN HERE!"
 840 IFMPG(14)=POZTHENPRINT"A PEA-SIZED EMERALD FALLS TO THE FLOOR!"
 845 IFMPG(15)=POZTHENPRINT"7 BARS OF SILVER LIE HERE!"
 847 IFPP(16)<>0THENPRINT"THE ";P$;" IS STILL BY YOUR SIDE.":GOTO860
 850 IFMPG(16)=POZTHENPRINT"A ";P$;" PATIENTLY WAITS HERE!"
 860 REM NEXT GOODIE
 885 LOOK=99:GOTO11015
 1005 PRINT"YOU`RE INSIDE A BUILDING,"
 1006 PRINT"A SAFE PLACE FOR TREASURES.":GOTO12000
 1008 PRINT"A SMALL BUILDING":GOTO12500
 1010 PRINT"YOU ARE ON A NICELY GROOMED PATH.":GOTO12000
 1013 PRINT"A SHALLOW VALLEY":GOTO12500
 1015 PRINT"YOU ARE IN THE FOREST.":IFPOZ<>15THEN12000
 1016 IFBLK<>0THEN1018
 1017 PRINTBK$;"IN IRON ARMOR STANDS HERE CARRYING A GOLDEN SWORD!":GOTO12000
 1018 PRINT"AN IRON STATUE OF ";BK$;"STANDS HERE.":GOTO12000
 1020 PRINTYI$;"A DRY STREAM BED OF A ONCE GREAT RIVER.":GOTO12000
 1023 PRINT"A RIVER BED":GOTO12500
 1025 PRINTYI$"THE BOTTOM OF A CLIMBABLE PIT. A SMALL STREAM"
 1026 PRINT"FLOWS ACROSS THE FLOOR OF THE PIT.":GOTO12000
 1028 PRINT"PIT":GOTO12500
 1030 PRINTYI$;"A BEAUTIFUL FOREST WHICH SOUNDS OF MANY ANIMALS.":GOTO12000
 1033 PRINT"A BEAUTIFUL FOREST":GOTO12500
 1035 PRINTYA$;"THE ENTRANCE OF A SMALL CAVE. A 3X3 IRON GRATE"
 1036 PRINT"KEEPS YOU FROM GOING DOWN WHERE THE CAVE OPENS UP.":GOTO12000
 1038 PRINT"A COBBLE PATH LEADS UP TO THE SURFACE":GOTO12500
 1040 PRINT"YOU ARE INSIDE THE CAVE ENTRANCE. IT IS A GOOD SIZED"
 1041 PRINT"PASSAGE EASY TO WALK IN.":GOTO12000
 1043 PRINT"A COBBLE PATH LEADS DOWN INTO THE CAVE":IFMG(1)<>0THEN12500
 1044 PRINT"THE GRATE IS OPEN.":GOTO12500
 1045 PRINTYA$;"THE EDGE OF A PIT. YOU CAN CLIMB DOWN BUT YOU"
 1046 PRINT"MIGHT NOT BE ABLE TO CLIMB BACK UP.":GOTO12000
 1050 PRINTYI$;"AN OLD MINE SHAFT THAT EXTENDS VERTICALLY BOTH WAYS."
 1051 GOTO12000
 1055 PRINT"YOU ARE STANDING BESIDE A LARGE SIGN FLASHING ";RV$;MW$;PV$;" !"
 1056 SAY=99:GOTO12000
 1060 PRINTYI$;"AN E/W TUNNEL.":GOTO12000
 1065 PRINTYI$;"AN E/W CORRIDOR.":GOTO12000
 1070 PRINTYI$;"A LARGE N/S PASSAGE.":GOTO12000
 1075 PRINTYI$;"A SMALL N/S CAVE.":GOTO12000
 1080 PRINTYI$;"A ROOM THAT CONNECTS TO AN ALTERNATE PASSAGE.":GOTO12000
 1085 PRINTYA$;"AN INTERSECTION OF THREE CORRIDORS.":GOTO12000
 1090 PRINTYA$;"THE BRINK OF AN EASILY CLIMBABLE PIT.":PRINT
 1092 PRINT"I THINK I HEAR THE SOUND OF WATER?":GOTO12000
 1095 PRINTYA$;"THE BOTTOM OF A NARROW SHAFT. YOU COULD CLIMB"
 1096 PRINT"UP BUT NOT BACK DOWN.":GOTO12000
 1100 PRINTYI$;"AN ENORMOUS CHAMBER OF SOLID GRANITE."
 1101 PRINT"EXITS LEAD IN MANY DIRECTIONS.":GOTO12000
 1105 PRINTYI$;"A CAVE OF MANY EXITS.":GOTO12000
 1110 PRINTYI$;"A BEAUTIFUL CAVERN. IN THE CENTER OF THE CAVERN IS A"
 1111 PRINT"HUGE HOLE!":PRINT:PRINT:GOTO12000
 1115 A$=" A SMALL CRACK ":IFFNR(99)>50THENA$=" A NARROW TUNNEL "
 1116 PRINT"YOU CLIMB THRU";A$;"ONLY TO FIND THAT.....":POZ=POZ-7
 1117 GOTO11001
 1120 PRINTYI$;"A SPLENDID CHAMBER DECORATED WITH STRANDS OF"
 1121 PRINT"MULTI-COLORED THREAD.":GOTO12000
 1125 POZ=POZ-5:GOTO10000
 1130 A$="A LITTLE, TWISTY ":IFFNR(99)>50THENA$="A TWISTY, LITTLE "
 1131 PRINTYI$;A$;"MAZE!":GOTO12000
 1135 PRINTYA$;"A COMPLEX JUNCTION OF THREE FORKS."
 1136 PRINT"OBVIOUS PATHS LEAD EAST AND WEST.":GOTO12000
 1140 PRINTYA$;"THE DRAGON`S LAIR. HIS JOB IS TO PROTECT THE MAGIC CASTLE"
 1141 PRINT"TO THE SOUTH.";:A$="REFUSES TO LET YOU ENTER."
 1142 IFDG<>0THENA$="LIES DEAD OFF TO ONE SIDE."
 1144 PRINT" THE DRAGON ";A$:GOTO12000
 1145 PRINTYI$;"A SMALL STONE CHAMBER OF THE MAGIC CASTLE.":IFFR<>0THEN12000
 1146 PRINT"A SMALL GREEN FROG HAPPILY HOPS ABOUT ON THE FLOOR.":GOTO12000
 1150 PRINTYA$;"ONE SIDE OF A LARGE MOAT WHICH SURROUNDS THE CASTLE."
 1151 PRINT"YOU CANNOT JUMP OR SWIM ACROSS."
 1152 IFRG<>0THENPRINT"THE DRAWBRIDGE IS LOWERED TO CROSS."
 1153 GOTO12000
 1155 PRINTYI$;"THE WEST HALL OF THE MAGIC CASTLE."
 1156 PRINT"YOUR FOOTSTEPS ECHO ENDLESSLY.":GOTO12000
 1160 PRINTYI$;"THE EASTERN HALL OF THE MAGIC CASTLE.":GOTO12000
 1165 PRINTYI$;"A ROOM WHERE A BED IS MADE FROM 12 MATRESSES.":GOTO12000
 1170 PRINT"SHHHH...";YI$;"THE KING`S CHAMBER."
 1171 PRINT"I THINK HE`S ASLEEP?":GOTO12000
 1175 PRINT"THE FLOOR YOU WERE STANDING ON HAS GIVEN WAY...YOU FALL..."
 1176 PRINT"AND FALL...AND FALL...FINALLY LANDING ON SOLID GROUND....."
 1177 POZ=POZ+25:GOTO10000
 1180 PRINT"WELCOME TO THE MAGIC CASTLE. ALTHOUGH OUR RULER IS STUPID"
 1181 PRINT"(KING DUMB) I HOPE YOU ENJOY YOUR VISIT.":GOTO12000
 2000 IFB$=P$AND(PP(16)<>0ORMPG(16)=POZ)THEN2010
 2001 GOSUB650:IFTEST<>0THEN11261
 2002 GOSUB600:IFTEST<>0THEN11261
 2003 IFPOZ<>93ANDPOZ<>91THEN12889
 2004 IFB$<>"FROG"ANDB$<>P$THEN11148
 2010 IFB$="FROG"THEN2050
 2011 IFMPG(16)<>POZTHEN12889
 2012 PRINT"THE ";P$;" DISAPPEARS IN A BLUE ";RV$;"*POOF*";PV$;" OF SMOKE"
 2015 PRINT:PRINT"A SMALL GREEN FROG LEAPS FROM THE SMOKE AND QUICKLY"
 2020 PRINT"HOPS OFF SOMEWHERE OUT OF SIGHT!"
 2025 PRINT:FR=0:MPG(16)=0:PP(16)=0:NF=99:GOTO11010
 2050 IFNF<>0THEN11261
 2051 PRINT"THE ROOM IS FILLED WITH BLUE SMOKE AND WHEN THE SMOKE CLEARS..."
 2055 PRINT:FR=99:MPG(16)=POZ:GOTO800
 2100 PRINT"THE ";P$;" REFUSES TO GO BY FORCE!":GOTO11010
 2200 PRINT"THE ";P$;" ACCEPTS YOUR OFFER AND WILL WALK AT YOUR SIDE."
 2210 PP(16)=99:MPG(16)=0:GOTO11010
 2300 PP(16)=0:MPG(16)=POZ:LOOK=0:GOTO800
 2400 PRINT"THEY ARE TOO HEAVY TO CARRY.":GOTO11010
 2500 HLP=HLP+1:IFHLP<FNR(HLP)+HLPOR(HLP<2ANDPP(2)<>0)THEN2510
 2502 PRINT"THE WIZARD SAYS, `QUIT BOTHERING ME!`":IFHLP>2THEN2520
 2505 GOTO11010
 2510 IFNL=99ANDHLP<4THEN2515
 2512 PRINT"NOTHING HAPPENS.":GOTO11010
 2515 PRINT"THE WIZARD OF `ADVENTURE` WILL LIGHT YOUR WAY FOR 25 MOVES."
 2516 NL=0:LIGHT=99:OIL=OIL+25
 2518 MOVES=225:LOOK=0:GOTO10000
 2520 PRINT"I, THE WIZARD OF `ADVENTURE` HAD GIVEN YOU ENOUGH WARNINGS!!"
 2522 PRINT"NOW YOU WILL SUFFER THIS FATE........"
 2525 PRINT:PRINT"THE WIZARD TURNS YOU INTO STONE!!!":PIT=PIT+1
 2530 PRINT:GOTO14010
 3000 IFDN<>0ORPOZ=91ORPOZ=93ORPP(16)<>0ORPOZ=87ORB$="MYSELF"THEN3010
 3001 IFB$="ME"ORB$="KNIGHT"THEN3010
 3002 GOSUB600:IF TEST<>0THEN11148
 3005 GOTO12889
 3010 IFB$="ME"ORB$="MYSELF"THEN3020
 3012 PRINT"WITH WHAT? YOUR BARE HANDS?":GOTO11010
 3020 PRINT"ARE YOU SURE YOU WANT ME TO END YOUR MISERABLE LIFE?"
 3022 INPUT"YES OR NO";A$
 3030 IFA$="YES"ORA$="NO"THEN3040
 3035 PRINT:PRINT"ANSWER `YES` OR `NO` DUCK-BRAIN!!!":PRINT:GOTO3020
 3040 IFMID$(A$,1,1)="Y"THEN14010
 3044 PRINT"OK!":GOTO11001
 4000 IFPOZ<>91ANDPOZ<>93THEN12889
 4002 PRINT"THE FROG HOPS JUST OUT OF REACH."
 4010 PRINT"IT SEEMS IMPOSSIBLE TO CATCH HIM!":GOTO11010
 5000 IFDG<>0THEN11246
 5001 IFPP(11)=0THEN12900
 5005 PRINT"THE DRAGON DODGES OUT OF THE WAY AND PROMPTLY"
 5006 PRINT"SITS ON THE AXE WHERE YOU CANNOT GET IT!"
 5010 NA=99:PP(11)=0:MPG(11)=87:PC=PC-1:GOTO11010
 5020 PRINT"THE DRAGON IS SITTING ON IT!":GOTO11010
 5100 IFPP(6)=0THENPRINT"YOU HAVE NOTHING THAT CAN KILL IT!":GOTO11010
 5110 PRINT"THE GOLDEN SWORD STRIKES HOME!":NA=0:LOOK=0:DR(87)=9915
 5111 DG=99:GOTO10000
 6000 PRINT"THE WATER QUICKLY RUSTS THE ARMOR AND TURNS"
 6004 PRINT"THE KNIGHT INTO AN IRON STATUE!"
 6010 PRINT"HE DROPS THE GOLDEN SWORD!":MPG(6)=15:PC=PC-1
 6020 MPG(5)=59:LOOK=0:BLK=99:GOTO800
 10000 IFFNR(99)>92ANDPOZ>25THEN700
 10010 IFMOVES>200ANDDL<>0THEN11000
 10020 IFMOVES>250ANDNL=0THEN10040
 10030 GOTO10050
 10040 PRINT"YOUR LANTERN HAS RUN OUT OF FUEL!":PRINT:PRINT:NL=99
 10050 IFNL<>0THENLIGHT=0
 10052 IFPOZ>24ANDPP(2)=0ANDMPG(2)<>POZTHENLIGHT=0
 10060 IFPOZ<25ORLIGHT<>0THEN11001
 10070 PRINT"IT IS PITCH DARK! IF YOU PROCEED YOU WILL LIKELY"
 10080 PRINT"FALL INTO A PIT!":IFFNR(99)>85THEN14000
 10090 GOTO11010
 11000 PRINT"YOUR LANTERN IS GETTING VERY DIM! LOOK FOR OIL!":DL=0
 11001 ONMZ(POZ)GOTO1005,1010,1015,1020,1025,1030,1035,1040,1045,1050
 11002 ONMZ(POZ)-10GOTO1055,1060,1065,1070,1075,1080,1085,1090,1095,1100
 11003 ONMZ(POZ)-20GOTO1105,1110,1115,1120,1125,1130,1135,1140,1145,1150
 11004 ONMZ(POZ)-30GOTO1155,1160,1165,1170,1175,1180
 11010 CNT=0:IFLOOK<>0THEN11015
 11011 FORI=1TO20:IFMPG(I)=POZTHENCNT=CNT+1
 11012 NEXT:IFPOZ=3THEN800
 11013 IFLIGHT<>0ANDCNT<>0THEN800
 11015 PRINT:INPUT"COMMAND";A$:IFA$=""THEN11395
 11016 IFLEN(A$)>0ANDMID$(A$,1,1)=" "THEN11395
 11017 MOVES=MOVES+1
 11018 IFPP(16)<>0THENMPG(16)=POZ
 11020 N=0:E=0:S=0:W=0:U=0:D=0
 11025 K=LEN(A$)
 11030 FORI=1TOK
 11035 IFMID$(A$,I,1)=" "THEN11045
 11040 NEXT
 11045 B$="":IFK+1=IORK=ITHEN11055
 11050 B$=MID$(A$,I+1)
 11055 A$=MID$(A$,1,I-1)
 11100 IFLEN(A$)>1THEN11140
 11105 IFMID$(A$,1,1)="N"THENNP=POZ-5:N=1:GOTO11135
 11110 IFMID$(A$,1,1)="E"THENNP=POZ+1:E=1:GOTO11135
 11115 IFMID$(A$,1,1)="S"THENNP=POZ+5:S=1:GOTO11135
 11120 IFMID$(A$,1,1)="W"THENNP=POZ-1:W=1:GOTO11135
 11125 IFMID$(A$,1,1)="U"THENNP=POZ-25:U=1:GOTO11135
 11130 IFMID$(A$,1,1)<>"D"THEN11140
 11131 NP=POZ+25:D=1
 11135 DIR=FNO(POZ):IFMID$(A$,1,1)<>"D"THEN11400
 11136 FORI=1TO2:IFMG(I)<>POZTHENNEXT:GOTO11400
 11137 PRINT"IT IS LOCKED!":GOTO11010
 11140 IFA$<>"WALK"ANDA$<>"CRAWL"ANDA$<>"JUMP"ANDA$<>"GO"THEN11150
 11141 IFLEN(B$)=0THENPRINTA$;" WHERE";:INPUTB$
 11143 IFB$<>"NORTH"ANDB$<>"EAST"ANDB$<>"SOUTH"ANDB$<>"WEST"THEN11148
 11145 A$=B$:GOTO11105
 11148 PRINT"C`MON, CAN`T YOU BE SERIOUS?":GOTO11010
 11150 IFA$="NORTH"ORA$="EAST"ORA$="SOUTH"ORA$="WEST"THEN11105
 11151 IFA$="DOWN"ORA$="UP"THEN11105
 11155 IFA$="LOOK"THENPRINT"I`M NOT ALLOWED TO GIVE MORE DETAIL.":GOTO10000
 11158 IFA$<>"EXIT"ANDA$<>"ENTER"THEN11178
 11160 IFPOZ=3ORPOZ=8THEN11166
 11162 PRINT"I`M NOT REALLY SURE WHICH WAY YOU`RE FACING."
 11164 PRINT"TRY A COMPASS HEADING.":GOTO11010
 11166 IFPOZ=8THEN11172
 11168 IFA$<>"EXIT"THEN11176
 11170 POZ=8:GOTO10000
 11172 IFA$="ENTER"THENPOZ=3:PRINT"YOU WALK THRU A WOODEN DOOR...":GOTO10000
 11176 PRINT"YOU DO NOT SEEM TO KNOW WHERE YOU ARE GOING!":GOTO11010
 11178 IFA$="INV"ORA$="INVENTORY"THEN12700
 11180 IFA$="GET"THEN12800
 11182 IFA$<>"LIGHT"ANDA$<>"LITE"THEN11198
 11184 IFNL<>0THEN11190
 11186 IFLEN(B$)<>0THEN11192
 11188 GOTO11395
 11190 IFNL<>0THENPRINT"YOUR LANTERN IS OUT OF OIL!":GOTO10000
 11192 IFB$<>"LANTERN"THEN11148
 11194 IFPP(2)=0THENPRINT"YOU DON`T HAVE A LANTERN!":GOTO11010
 11196 DL=99:LIGHT=99:PRINT"YOUR LANTERN IS LIT!":GOTO10000
 11198 IFA$<>"QUIT"ANDB$<>"QUIT"THEN11212
 11200 PRINT:PRINT"DO YOU REALLY WANT TO QUIT";:INPUTA$
 11202 IFLEN(A$)<4THEN11208
 11204 PRINT"ANSWER `YES` OR `NO` COWARDLY KNAVE!"
 11206 INPUT"DO YOU WANT TO QUIT";A$
 11208 IFMID$(A$,1,1)="Y"THEN30000
 11210 GOTO10000
 11212 IFA$="DROP"THEN12900
 11214 IFA$<>"SAY"THEN11224
 11216 IFSAY<>0THEN13000
 11218 IFLEN(B$)<>0THEN11222
 11220 PRINTA$;" WHAT";:INPUTB$
 11222 PRINT"OKAY, `";B$;"`!":GOTO11010
 11224 IFA$<>"OPEN"ANDA$<>"UNLOCK"THEN11230
 11225 IFPP(1)=0THENPRINT"YOU HAVE NO KEYS!":GOTO11010
 11227 FORI=1TO2:IFMG(I)=POZTHEN11229
 11228 NEXT:PRINT"THERE`S NOTHING HERE TO UNLOCK!":GOTO11010
 11229 MG(I)=0:PRINT"THE KEYS HAVE ";A$;"ED IT!":GOTO11010
 11230 IFA$<>"DRINK"THEN11235
 11231 IFPP(5)=0ORB$<>"WATER"THEN11233
 11232 PRINT"THANKS, I WAS VERY THIRSTY!":PP(5)=0:PC=PC-1:GOTO11010
 11233 IFB$<>"WATER"THEN11148
 11234 PRINT"YOU HAVE TO GET IT FIRST!":GOTO11010
 11235 IFA$<>"EAT"THEN11240
 11236 GOSUB650:IFI<3ORPP(I)=0THEN11239
 11237 PC=PC-1:PRINT"THANKS, DON`T MIND IF I DO!":PRINT
 11238 PP(I)=0:PRINT"YOUR ";B$;" WAS VERY TASTY!":GOTO11010
 11239 PRINT"I`M GAME. WOULD YOU CARE TO EXPLAIN HOW?":GOTO11010
 11240 IFA$<>"THROW"THEN11253
 11241 IFB$<>"AXE"THEN12900
 11242 IFPOZ=87THEN5000
 11243 IFPP(11)<>0THEN11246
 11244 GOTO12900
 11245 PRINT"THERE`S NOTHING HERE TO ATTACK!":GOTO11010
 11246 IFPOZ=15THEN12900
 11247 IFDN=0THEN11245
 11248 PP(11)=0:A$="":PC=PC-1:MPG(11)=POZ:B$="YOU MISSED!"
 11249 B$=B$+" THE FRIGHTENED DWARF RAN AWAY!":IFFNR(99)>70THEN11252
 11250 A$="YOU KILLED A LITTLE DWARF. HIS BODY"
 11251 B$="VANISHES IN A "+RV$+"*POOF*"+PV$+" OF GREASY SMOKE!"
 11252 PRINTA$:PRINTB$:DWF=FNR(3)+1:LOOK=0:PRINT:DN=0:GOTO10000
 11253 IFA$<>"WAVE"ANDA$<>"SHAKE"THEN11264
 11254 IFPP(8)=0ORPOZ<>92ORB$<>"WAND"THEN11256
 11255 RG=99:DR(92)=9907:GOTO11001
 11256 GOSUB650:IFTEST<>0THEN11261
 11257 PRINT"YOU DON`T HAVE THAT TO ";A$;"!":GOTO11010
 11258 DN=0:DWF=FNR(2)+1:PRINT"THE ANGRY LITTLE DWARF SEES THE ANGER"
 11259 PRINT"IN THE WAY THAT YOU ";A$;" YOUR AXE"
 11260 PRINT"AND PROMPTLY HEADS FOR THE NEAREST EXIT!":GOTO11010
 11261 IFDN<>0ANDB$="AXE"THEN11258
 11262 PRINT"NOTHING HAPPENS!":GOTO11010
 11264 IFA$<>"PAY"THEN11269
 11265 IFPP(12)=0THENPRINT"NO MONEY...NO BOOK!":GOTO11010
 11266 IFPOZ<>170THEN11395
 11267 PRINT"THANK YOU SIR, I HOPE YOU ENJOY IT!":PP(14)=99
 11268 MPG(14)=0:PC=PC+1:FINE=99:GOTO11010
 11269 IFA$<>"GIVE"THEN11275
 11270 IFPP(5)<>0ANDPOZ=145ANDB$="WATER"THEN11273
 11271 IFPP(5)=0THEN12900
 11272 PRINT"WHO ARE YOU GOING TO GIVE IT TO?":GOTO11010
 11273 PRINT"THE OLD MAN DRINKS THE WATER AND WALKS OFF INTO THE DARKNESS!"
 11274 PP(5)=0:PC=PC-1:MZ(145)=28:GOTO10000
 11275 IFA$<>"WET"THEN11280
 11276 IFPP(5)=0THENPRINT"WITH WHAT? SPIT? SWEAT?":GOTO11148
 11277 IFPOZ<>157THEN11395
 11278 PRINT"YOU SLIP AND SLIDE INTO......":PRINT:PP(5)=0
 11279 DR(157)=9908:PC=PC-1:POZ=159:GOTO10000
 11280 DN=0:IFA$<>"READ"ORPP(14)=0THEN11283
 11281 IFB$<>"BOOK"THEN11148
 11282 PRINT"THE BOOK READS, `SAY MAGIC WORD IN THE `NOT SAME` MAZE!`"
 11283 IFA$="KILL"THEN3000
 11284 IFA$<>"WITH"THEN11288
 11285 IFB$<>"AXE"THEN11148
 11286 A$="THROW":B$="AXE":GOTO11240
 11288 IFA$=MW$THENB$=MW$:A$="SAY":GOTO11214
 11290 IFA$="WHY"THENPRINT"BECAUSE I SAID SO! THAT`S WHY!!!":GOTO11010
 11291 IFA$="FUCK"ORA$="SHIT"THENPRINT"WATCH IT!!":GOTO11293
 11292 IFA$<>"YOU"ANDA$<>"YOUR"ANDB$<>"YOU"THEN11296
 11293 PRINT"IF YOU DON`T LIKE PLAYING THE GAME,"
 11294 PRINT"YOU CAN ALWAYS `QUIT`!":GOTO11010
 11296 IFA$<>"FIND"THEN11300
 11297 PRINT"I CANNOT TELL YOU WHERE REMOTE THINGS ARE LOCATED!":GOTO11010
 11300 IFA$="SLAY"ANDB$="DRAGON"THEN5100
 11305 IFA$<>"YES"THEN11310
 11306 IFPOZ=15ORPOZ=87ORDN<>0THEN11148
 11308 PRINT"YOU SEEM UNSURE OF THAT ANSWER!":GOTO11010
 11310 IFA$<>"LIFT"THEN11320
 11312 GOSUB600:IFTEST=0ANDPOZ<>99THEN12889
 11315 IFB$<>"MATRESS"ANDB$<>"MATRESSES"THEN11261
 11318 MPG(14)=99:LOOK=0:GOTO800
 11320 IFA$="KISS"THEN2000
 11325 IFA$="PLEASE"ANDMPG(16)=POZTHEN2200
 11330 IFA$="WAIT"ANDPP(16)<>0THEN2300
 11335 IFA$="HELP"THEN2500
 11340 IFA$<>"LISTEN"THEN11345
 11342 PRINT"SLOSH...BOIL...DRIP...SPURT...SQUIRT...":GOTO11010
 11345 IFA$="WHO"ORA$="WHAT"ORA$="WHEN"ORA$="WHERE"ORA$="HOW"THEN11350
 11348 GOTO11355
 11350 PRINT"I REFUSE TO ANSWER ON THE GROUNDS THAT"
 11352 PRINT"IT MIGHT INCRIMINATE ME!":GOTO11010
 11355 IFMID$(A$,1,1)="N"THENPRINT"MUST YOU BE SO NEGATIVE?":GOTO11010
 11360 IFA$<>"CAUSE"ANDA$<>"BECAUSE"THEN11368
 11362 PRINT"THAT ANSWER STINKS, DONKEY-BRAINED BOTTOM BITER!":GOTO11010
 11368 REM DO NOT UNDERSTAND
 11380 IFB$=""THENQ=99
 11382 B$=A$:GOSUB650:IFTEST=0THEN11395
 11384 PRINT"WHAT DO YOU WANT TO DO WITH THE ";B$;:INPUTA$:GOTO11180
 11390 REM DO NOT UNDERSTAND
 11395 B$="I DON`T UNDERSTAND WHAT YOU MEAN!"
 11396 IFFNR(99)>50THENB$="WHAT???"
 11397 IFQ<>0THENPRINT"I DON`T KNOW THAT WORD!":GOTO11010
 11398 PRINTB$:GOTO11010
 11400 IFMZ(POZ)<>6THEN11430
 11401 IFN=0THEN11405
 11402 N=0:E=1:NP=POZ+1:GOTO11450
 11405 IFE=0THEN11410
 11406 E=0:S=1:NP=POZ+5:GOTO11440
 11410 IFS=0THEN11420
 11412 S=0:W=1:NP=POZ-1:GOTO11460
 11420 IFW=0THEN11430
 11422 W=0:N=1:NP=POZ-5
 11430 IFN=0THEN11440
 11432 IFDIR=1ORDIR=7ORDIR=12ORDIR=14ORDIR=15ORDIR=16THEN11550
 11434 IFDIR=17ORDIR=19THEN11550
 11436 GOTO11500
 11440 IFS=0THEN11450
 11442 IFDIR=3ORDIR=7ORDIR=11ORDIR=13ORDIR=15ORDIR=16THEN11550
 11444 IFDIR=18ORDIR=19THEN11550
 11446 GOTO11500
 11450 IFE=0THEN11460
 11452 IFDIR=2ORDIR=8ORDIR=9ORDIR=10ORDIR=11ORDIR=12THEN11550
 11454 IFDIR=15ORDIR=16ORDIR=17ORDIR=18THEN11550
 11456 GOTO11500
 11460 IFW=0THEN11470
 11462 IFDIR=4ORDIR=8ORDIR=13ORDIR=14ORDIR=15ORDIR=17THEN11550
 11464 IFDIR=18ORDIR=19THEN11550
 11466 GOTO11500
 11470 IFD=0THEN11480
 11472 IFDIR=5ORDIR=6ORDIR=9THEN11550
 11474 GOTO11500
 11480 IFU=0THEN11500
 11482 IFDIR=0ORDIR=6ORDIR=10THEN11550
 11500 PRINT:IFPOZ=87THEN11510
 11502 GOTO11176
 11510 A$="REFUSES TO LET YOU PASS!":GOTO1142
 11550 POZ=NP:LOOK=0:GOTO10000
 12000 N=0:E=0:S=0:W=0:U=0:D=0
 12005 IFMOVES>50+FNR(50)THENGOSUB200
 12010 L=INT(DR(POZ)/100)
 12020 IFL=0THENU=1:GOTO12500
 12030 IFL=1THENN=1:GOTO12500
 12040 IFL=2THENE=1:GOTO12500
 12050 IFL=3THENS=1:GOTO12500
 12060 IFL=4THENW=1:GOTO12500
 12070 IFL=5THEND=1:GOTO12500
 12080 IFL=6THEND=1:U=1:GOTO12500
 12090 IFL=7THENN=1:S=1:GOTO12500
 12100 IFL=8THENE=1:W=1:GOTO12500
 12110 IFL=9THEND=1:W=1:GOTO12500
 12120 IFL=10THENU=1:E=1:GOTO12500
 12130 IFL=11THENS=1:E=1:GOTO12500
 12140 IFL=12THENN=1:E=1:GOTO12500
 12150 IFL=13THENW=1:S=1:GOTO12500
 12160 IFL=14THENW=1:N=1:GOTO12500
 12170 IFL=15THENW=1:N=1:E=1:S=1:GOTO12500
 12180 IFL=16THENN=1:S=1:E=1:GOTO12500
 12190 IFL=17THENN=1:W=1:E=1:GOTO12500
 12200 IFL=18THENS=1:W=1:E=1:GOTO12500
 12210 IFL=19THENS=1:W=1:N=1:GOTO12500
 12220 REM LOOK AROUND
 12500 IFN<>0THENLT=POZ-5:N=0:PRINT"TO THE NORTH ";:GOTO12570
 12510 IFE<>0THENLT=POZ+1:E=0:PRINT"ALONG THE EAST ";:GOTO12570
 12520 IFS<>0THENLT=POZ+5:S=0:PRINT"LOOKING SOUTH ";:GOTO12570
 12530 IFW<>0THENLT=POZ-1:W=0:PRINT"APPEARING WESTERLY ";:GOTO12570
 12540 IFU<>0THENLT=POZ-25:U=0:GOTO12570
 12550 IFD<>0THENLT=POZ+25:D=0:GOTO12570
 12560 GOTO11010
 12570 ONMZ(LT)GOTO1008,1013,1018,1023,1028,1033,1038,1043
 12580 GOTO11010
 12700 PRINT:PRINT"YOU ARE HOLDING THE FOLLOWING ITEMS:":PRINT
 12701 FORI=1TO16:IFI=16ANDPP(16)<>0THEN12705
 12702 IFPP(I)<>0THENPRINTMID$(THG$,THG(I),THG(I+1)-1-THG(I))
 12705 NEXT
 12795 FORI=1TO20:IFPP(I)<>0THEN11010
 12798 NEXT:PRINT"NOTHING.":GOTO11010
 12800 CNT=0:FORI=1TO20:IFMPG(I)=POZTHENCNT=CNT+1
 12802 NEXT
 12810 IFPC<6THEN12815
 12811 PRINT"YOU CAN`T CARRY ANYTHING MORE."
 12812 PRINT"YOU`LL HAVE TO DROP SOMETHING FIRST!":GOTO11010
 12815 IFLEN(B$)=0THENPRINTA$;:INPUT" WHAT";B$
 12820 GOSUB600
 12825 IFTEST<>0THEN12842
 12826 IFB$="WATER"ANDPOZ=59THEN12842
 12827 IFB$="DRAGON"THEN11148
 12828 IFB$="FROG"THEN4000
 12830 IFB$="MATRESS"ORB$="MATRESSES"THEN2400
 12832 IFMPG(16)=POZTHEN2100
 12840 GOTO12889
 12842 IFB$<>"WATER"ORMPG(5)<>POZTHEN12850
 12843 IFPP(5)<>0THEN12847
 12844 IFPP(4)<>0THENPP(5)=99:GOTO12890
 12846 PRINT"YOU HAVE NOTHING TO CARRY THE WATER IN!":GOTO11010
 12847 PRINT"YOU CAN`T CARRY ANY MORE WATER!":GOTO11010
 12850 IFB$<>"OIL"ORMPG(12)<>POZTHEN12874
 12852 MPG(12)=0:PRINT"YOUR LANTERN IS FULL AGAIN!"
 12854 OIL=MOVES:DL=0:MOVES=1:GOTO11010
 12874 IFB$="BOOK"ANDFINE=0ANDPOZ=159THEN1231
 12875 IFB$<>"BOOK"ORMPG(14)<>POZTHEN12878
 12876 PP(14)=99:MPG(14)=0:GOTO12890
 12878 IFB$<>"TREASURE"ORMPG(7)<>POZTHEN12885
 12879 IFMG(2)=0THENPP(7)=99:MPG(7)=0:GOTO12890
 12880 PRINT"IT`S LOCKED!":GOTO11010
 12885 IFB$<>"AXE"THEN12888
 12886 IFNA<>0THEN5020
 12888 PP(I)=99:MPG(I)=0:GOTO12890
 12889 PRINT"I SEE NO ";B$;" HERE!":GOTO11010
 12890 PRINT"OK!":PC=PC+1:GOTO11010
 12900 IFPC<>0THEN12910
 12901 PRINT"YOU HAVE NOTHING TO ";A$;"!":GOTO11010
 12905 IFLEN(B$)=0THENPRINTA$;" WHAT?":GOTO11010
 12910 GOSUB650:IFTEST<>0THEN12920
 12911 IFB$="PRINCESS"THEN2010
 12919 PRINT"YOU ARE NOT CARRYING IT!":GOTO11010
 12920 IFB$="LANTERN"THENDL=0
 12922 IFB$<>"BOTTLE"ORPP(4)=0THEN12927
 12923 PP(4)=0:MPG(4)=POZ:IFPP(5)=0THEN12995
 12924 PP(5)=0
 12925 PRINT"THE BOTTLE TIPPED OVER AND THE WATER SPILLED OUT!"
 12926 PC=PC-1:GOTO12995
 12927 IFB$<>"STATUE"ORPP(6)=0THEN12933
 12928 IFMPG(7)=POZTHEN12930
 12929 PRINT"THE STATUE FALLS AND BREAKS WORTHLESSLY!":GOTO11932
 12930 PP(6)=0:MPG(6)=POZ:IFBREAK<>0THEN11010
 12931 PRINT"THE STATUE LANDS SAFELY ON THE PERSIAN RUG!":GOTO12995
 12932 PP(6)=0:MPG(6)=0:GOTO12995
 12933 PP(I)=0:MPG(I)=POZ
 12935 IFB$="WATER"ANDA$="THROW"ANDPOZ=15ANDBLK=0THEN6000
 12995 PRINT"OK!":PC=PC-1:GOTO11010
 13000 IFB$<>MW$THEN11222
 13010 GOTO13030
 13020 PRINT"A SECRET PASSAGE OPENS TO THE WEST!":DR(199)=9914:GOTO11010
 13030 IFPOZ=3THENPOZ=41:GOTO13037
 13035 IFPOZ<>41THEN13040
 13036 POZ=3
 13037 LOOK=0
 13040 IFPOZ=3ORPOZ=41THEN10000
 13042 PRINT"NOTHING HAPPENS!":GOTO11010
 14000 PRINT:PRINT"YOU FELL INTO A PIT AND ARE"
 14002 PRINT"A MASS OF TWISTED FLESH AND BONES!":PRINT
 14010 PIT=PIT+1:IFPIT>2THEN14030
 14020 PRINT:PRINT"I MIGHT BE ABLE TO REINCARNATE YOU!"
 14022 PRINT"WOULD YOU LIKE ME TO TRY";:GOTO14050
 14030 PRINT"YOU`VE REALLY DONE IT THIS TIME! I`M ALL OUT OF FAIRY DUST!"
 14035 PRINT:PRINT"YOU DON`T REALLY EXPECT ME TO DO A"
 14036 PRINT"DECENT REINCARNATION WITHOUT IT DO YOU";
 14037 KILL=0:DWF=0
 14050 IFPIT>3THEN14070
 14051 INPUTA$:IFMID$(A$,1,1)="N"THEN30000
 14052 PRINT"READY...ONE...TWO...THREE...";RV$;"*POOF*";PV$
 14055 PRINT:PRINT"YOU EMERGE FROM A CLOUD OF FAIRY DUST TO FIND YOURSELF..."
 14060 LOOK=0:FORI=1TO20:IFI=2THEN14063
 14061 IFPP(I)<>0THENMPG(I)=POZ
 14063 NEXT:FORI=1TO20:PP(I)=0:NEXT:POZ=3:MPG(2)=3:PC=0:GOTO11001
 14070 INPUTA$:IFMID$(A$,1,1)="N"THEN30000
 14080 PRINT:PRINT"OK! IF YOU`RE SO SMART, DO IT YOURSELF!!":PRINT
 30000 PTS=0:FORI=1TO5:IFMPG(I)=3THENPTS=PTS+10
 30004 NEXT
 30010 FORI=6TO10:IFMPG(I)=3THENPTS=PTS+20
 30012 NEXT
 30015 FORI=11TO15:IFMPG(I)=3THENPTS=PTS+30
 30016 NEXT
 30017 IFMPG(16)=3THENPTS=PTS+50
 30020 IFSAY<>0THENPTS=PTS+50
 30030 IFOIL>0THENMOVES=MOVES+OIL
 30035 PTS=PTS-INT(MOVES/10)
 30040 PRINT:PRINT"YOU SCORED ";PTS;" POINTS OUT OF A"
 30042 PRINT"POSSIBLE 350, USING ";MOVES;" TURNS!"
 30045 A$=" RANK BEGINNER":IFPTS>50THENA$=" NOVICE"
 30046 IFPTS>100THENA$="N EXPERIENCED"
 30047 IFPTS>150THENA$=" SEASONED"
 30050 IFPTS>200THENA$=" JR. MASTER"
 30051 IFPTS>250THENA$=" SR. MASTER"
 30052 IFPTS>300THENA$=" JR. WIZARD"
 30053 IFPTS=>350THENA$=" "+RV$+"WIZARD"+PV$
 30055 PRINT"YOUR SCORE QUALIFIES YOU FOR A";A$;" ADVENTURER!"
 30060 FORI=50TO350STEP50:IFPTS-I>0THENNEXT
 30065 PRINT"TO ACHIEVE THE NEXT HIGHER RATING"
 30070 PRINT"YOU NEED ";I-PTS;" MORE POINTS!":END
