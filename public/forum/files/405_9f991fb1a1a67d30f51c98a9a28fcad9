0REM LISTING OF PROGRAM ADVENT
5CLEAR:FORI=1TO12:?:NEXT:PRINTTAB(25);"*** ADVENTURE ***"
6FORI=1TO10:?:NEXT:RESTORE
10DIMMZ(200),DR(200),MPG(20),PP(20),THG(17)
13THG$="KEYS LANTERN FOOD BOTTLE WATER SWORD TREASURE WAND ROPE "
14THG$=THG$+"DIAMONDS AXE OIL CROWN EMERALD SILVER "
15MG(1)=16:MG(2)=33:YA$="YOU ARE AT ":YI$="YOU ARE IN "
16MG(0)=MG(2):BK$="A BLACK KNIGHT ":RV$=CHR$(27)+CHR$(106)
17THG(1)=1:THG(2)=6:THG(3)=14:THG(4)=19:THG(5)=26:THG(6)=32:THG(7)=38
18THG(8)=47:THG(9)=52:THG(10)=57:THG(11)=66:THG(12)=70:THG(13)=74
19THG(14)=80:THG(15)=88:THG(16)=95:PV$=CHR$(27)+CHR$(107)
20FORI=1TO20:PP(I)=0:NEXT
25INPUT"ARE YOU A BOY OR A GIRL";A$
30IFA$="BOY"ORA$="GIRL"THEN35
32?"PLEASE ANSWER BOY OR GIRL!":FORI=1TO500:NEXT:GOTO25
35DEFFNR(Q)=INT(Q*RND(1))
36DEFFNO(X)=INT(DR(X)-100*INT(DR(X)/100))
40P$="PRINCESS":IFA$="GIRL"THENP$="PRINCE"
42MW$="XYZZY":IFFNR(99)>66THENMW$="CIGAM"
45IFFNR(99)>66THENMW$="RENOB"
100FORI=1TO100:READMZ(I):MZ(I+100)=-1:NEXT:MZ(0)=0
110FORI=1TO100:READDR(I):DR(I+100)=-1:NEXT:DR(0)=0
120FORI=0TO20:READMPG(I):NEXT
135FORI=1TO12:?:NEXT:PRINTTAB(25);"*** ADVENTURE ***":PRINT
136?:GOTO500
140DATA9,3,1,3,3,6,6,2,6,6,3,6,2,2,3,7,2,2,6,3,3,4,4,4,11,9,17,12
145DATA12,17,19,16,20,18,21,12,17,22,23,26,8,21,12,14,24,12,16,17
150DATA12,23,9,13,13,13,13,13,25,26,5,9,26,26,27,12,12,26,26,26
155DATA26,26,26,26,26,26,26,16,17,12,12,19,12,21,19,14,16,17,28,12
160DATA12,16,29,30,29,13,34,31,36,32,33,35
162DATA9909,9903,9999,9911,9918,9915,9915,1515,9915,9915,9917,9915
164DATA9915,9916,9919,509,9915,9915,9915,9917,9908,9908,9917,9917
166DATA9917,9909,9918,9908,9908,9918,9910,9914,9919,9909,9915,9908
168DATA9918,9909,9999,9913,10,9915,9908,9907,9915,9908,9914,9917
170DATA9908,9999,9909,9908,9908,9908,9908,9918,9999,9910,9900,9909
172DATA9915,9919,9918,9908,9908,9915,9919,9919,9915,9915,9912,9914
174DATA9917,9917,9917,9917,9918,9908,9908,9910,9908,9915,9910,9907
176DATA9913,9917,9917,9908,9908,9914,9903,9901,9911,9908,9908,9917
178DATA9917,9917,9904,9905
180DATA0,3,3,3,3,59,0,33,28,45,49,0,69,95,0,44,0,0,0,0,0
200IFPIR<>0THEN299
201IFPOZ=3THEN299
202IFSHDW<>0THEN210
204IFFNR(99)<96THEN299
206SHDW=99
210?:PRINT"A SHADOWY FIGURE IS FOLLOWING YOU!":PRINT
212FORI=16TO6STEP-1:IFPP(I)<>0ANDSW>FNR(10)+5THEN215
213NEXT:SW=SW+1:GOTO299
215?"THE SHADOW LEAPS OUT OF THE DARKNESS."
216?"`HAR HAR`, CRIES A BEARDED PIRATE, "
220?"`ME THINKS ME WILL TAKE A LITTLE OF YUR BOOTY!":PRINT
222IFI=16THENB$=P$:GOTO227
225B$=MID$(THG$,THG(I),THG(I+1)-1-THG(I))
227?"THAT ";B$;" WILL DO JUST FINE!`"
228?:PRINT"WITH THAT HE SCURRIES OFF TO HIS TREASURE CAVE!"
230?:PRINT:PP(I)=0:MPG(I)=33
240PIR=99
250PC=PC-1
299RETURN
500POZ=8:GOTO10000
600TEST=0:FORI=1TO15:IFMPG(I)<>POZTHEN610
605IFPP(I)<>0ORB$<>MID$(THG$,THG(I),THG(I+1)-1-THG(I))THEN610
607GOTO620
610NEXT:RETURN
620TEST=99:RETURN
650TEST=0:FORI=1TO15
655IFB$=MID$(THG$,THG(I),THG(I+1)-1-THG(I))ANDPP(I)<>0THEN620
658NEXT:RETURN
660TEST=99:RETURN
700IFD1<>0THEN710
702?"AN ANGRY DWARF CAME IN, THREW A LITTLE AXE, WHICH MISSED, "
703?"CURSED, AND RAN AWAY!":PRINT:PRINT
705MPG(11)=POZ:D1=99:?:GOTO10010
710IFD1<>0THENDWF=DWF+2:DN=99
715?"THERE`S AN ANGRY DWARF IN THE ROOM WITH YOU!"
717?"HE THROWS A SINGLE KNIFE!";
720IFFNR(DWF)<=5THEN?"  IT MISSES!":PRINT:GOTO10010
750?"  IT HITS YOU!":KILL=KILL+1:IFKILL>=2THEN14010
760?:GOTO11001
800?:IFMPG(1)=POZTHENPRINT"THERE`S A SET OF KEYS HERE!"
802IFMPG(2)=POZTHEN?"A LANTERN RESTS HERE!"
804IFMPG(3)=POZTHEN?"THERE IS TASTY FOOD HERE!"
805IFMPG(4)=POZTHEN?"A SMALL BOTTLE IS LAYING HERE!"
806IFMPG(5)=POZTHEN?"THERE IS MUCH WATER HERE!"
808IFMPG(6)=POZTHEN?"A GOLDEN SWORD LIES HERE!"
809IFMPG(7)=POZTHEN?"THERE`S A PIRATE`S TREASURE CHEST HERE!"
810IFMPG(8)=POZTHEN?"A MAGIC WAND FLOATS IN MID AIR!"
815IFMPG(9)=POZTHEN?"A ROPE OF GOLDEN THREAD IS COILED HERE!"
820IFMPG(10)=POZTHEN?"MANY DIAMONDS ARE SCATTERED HERE!"
825IFMPG(11)=POZANDNA=0THEN?"A LITTLE AXE IS HERE!"
830IFMPG(12)=POZTHEN?"A SMALL POOL OF OIL IS HERE!"
835IFMPG(13)=POZTHEN?"THERE`S A KING`S CROWN HERE!"
840IFMPG(14)=POZTHEN?"A PEA-SIZED EMERALD FALLS TO THE FLOOR!"
845IFMPG(15)=POZTHEN?"7 BARS OF SILVER LIE HERE!"
847IFPP(16)<>0THEN?"THE ";P$;" IS STILL BY YOUR SIDE.":GOTO860
850IFMPG(16)=POZTHEN?"A ";P$;" PATIENTLY WAITS HERE!"
860REM NEXT GOODIE
885LOOK=99:GOTO11015
1005?"YOU`RE INSIDE A BUILDING,"
1006?"A SAFE PLACE FOR TREASURES.":GOTO12000
1008?"A SMALL BUILDING":GOTO12500
1010?"YOU ARE ON A NICELY GROOMED PATH.":GOTO12000
1013?"A SHALLOW VALLEY":GOTO12500
1015?"YOU ARE IN THE FOREST.":IFPOZ<>15THEN12000
1016IFBLK<>0THEN1018
1017?BK$;"IN IRON ARMOR STANDS HERE CARRYING A GOLDEN SWORD!":GOTO12000
1018?"AN IRON STATUE OF ";BK$;"STANDS HERE.":GOTO12000
1020?YI$;"A DRY STREAM BED OF A ONCE GREAT RIVER.":GOTO12000
1023?"A RIVER BED":GOTO12500
1025?YI$"THE BOTTOM OF A CLIMBABLE PIT. A SMALL STREAM"
1026?"FLOWS ACROSS THE FLOOR OF THE PIT.":GOTO12000
1028?"PIT":GOTO12500
1030?YI$;"A BEAUTIFUL FOREST WHICH SOUNDS OF MANY ANIMALS.":GOTO12000
1033?"A BEAUTIFUL FOREST":GOTO12500
1035?YA$;"THE ENTRANCE OF A SMALL CAVE. A 3X3 IRON GRATE"
1036?"KEEPS YOU FROM GOING DOWN WHERE THE CAVE OPENS UP.":GOTO12000
1038?"A COBBLE PATH LEADS UP TO THE SURFACE":GOTO12500
1040?"YOU ARE INSIDE THE CAVE ENTRANCE. IT IS A GOOD SIZED"
1041?"PASSAGE EASY TO WALK IN.":GOTO12000
1043?"A COBBLE PATH LEADS DOWN INTO THE CAVE":IFMG(1)<>0THEN12500
1044?"THE GRATE IS OPEN.":GOTO12500
1045?YA$;"THE EDGE OF A PIT. YOU CAN CLIMB DOWN BUT YOU"
1046?"MIGHT NOT BE ABLE TO CLIMB BACK UP.":GOTO12000
1050?YI$;"AN OLD MINE SHAFT THAT EXTENDS VERTICALLY BOTH WAYS."
1051GOTO12000
1055?"YOU ARE STANDING BESIDE A LARGE SIGN FLASHING ";RV$;MW$;PV$;" !"
1056SAY=99:GOTO12000
1060?YI$;"AN E/W TUNNEL.":GOTO12000
1065?YI$;"AN E/W CORRIDOR.":GOTO12000
1070?YI$;"A LARGE N/S PASSAGE.":GOTO12000
1075?YI$;"A SMALL N/S CAVE.":GOTO12000
1080?YI$;"A ROOM THAT CONNECTS TO AN ALTERNATE PASSAGE.":GOTO12000
1085?YA$;"AN INTERSECTION OF THREE CORRIDORS.":GOTO12000
1090?YA$;"THE BRINK OF AN EASILY CLIMBABLE PIT.":PRINT
1092?"I THINK I HEAR THE SOUND OF WATER?":GOTO12000
1095?YA$;"THE BOTTOM OF A NARROW SHAFT. YOU COULD CLIMB"
1096?"UP BUT NOT BACK DOWN.":GOTO12000
1100?YI$;"AN ENORMOUS CHAMBER OF SOLID GRANITE."
1101?"EXITS LEAD IN MANY DIRECTIONS.":GOTO12000
1105?YI$;"A CAVE OF MANY EXITS.":GOTO12000
1110?YI$;"A BEAUTIFUL CAVERN. IN THE CENTER OF THE CAVERN IS A"
1111?"HUGE HOLE!":PRINT:PRINT:GOTO12000
1115A$=" A SMALL CRACK ":IFFNR(99)>50THENA$=" A NARROW TUNNEL "
1116?"YOU CLIMB THRU";A$;"ONLY TO FIND THAT.....":POZ=POZ-7
1117GOTO11001
1120?YI$;"A SPLENDID CHAMBER DECORATED WITH STRANDS OF"
1121?"MULTI-COLORED THREAD.":GOTO12000
1125POZ=POZ-5:GOTO10000
1130A$="A LITTLE, TWISTY ":IFFNR(99)>50THENA$="A TWISTY, LITTLE "
1131?YI$;A$;"MAZE!":GOTO12000
1135?YA$;"A COMPLEX JUNCTION OF THREE FORKS."
1136?"OBVIOUS PATHS LEAD EAST AND WEST.":GOTO12000
1140?YA$;"THE DRAGON`S LAIR. HIS JOB IS TO PROTECT THE MAGIC CASTLE"
1141?"TO THE SOUTH.";:A$="REFUSES TO LET YOU ENTER."
1142IFDG<>0THENA$="LIES DEAD OFF TO ONE SIDE."
1144?" THE DRAGON ";A$:GOTO12000
1145?YI$;"A SMALL STONE CHAMBER OF THE MAGIC CASTLE.":IFFR<>0THEN12000
1146?"A SMALL GREEN FROG HAPPILY HOPS ABOUT ON THE FLOOR.":GOTO12000
1150?YA$;"ONE SIDE OF A LARGE MOAT WHICH SURROUNDS THE CASTLE."
1151?"YOU CANNOT JUMP OR SWIM ACROSS."
1152IFRG<>0THEN?"THE DRAWBRIDGE IS LOWERED TO CROSS."
1153GOTO12000
1155?YI$;"THE WEST HALL OF THE MAGIC CASTLE."
1156?"YOUR FOOTSTEPS ECHO ENDLESSLY.":GOTO12000
1160?YI$;"THE EASTERN HALL OF THE MAGIC CASTLE.":GOTO12000
1165?YI$;"A ROOM WHERE A BED IS MADE FROM 12 MATRESSES.":GOTO12000
1170?"SHHHH...";YI$;"THE KING`S CHAMBER."
1171?"I THINK HE`S ASLEEP?":GOTO12000
1175?"THE FLOOR YOU WERE STANDING ON HAS GIVEN WAY...YOU FALL..."
1176?"AND FALL...AND FALL...FINALLY LANDING ON SOLID GROUND....."
1177POZ=POZ+25:GOTO10000
1180?"WELCOME TO THE MAGIC CASTLE. ALTHOUGH OUR RULER IS STUPID"
1181?"(KING DUMB) I HOPE YOU ENJOY YOUR VISIT.":GOTO12000
2000IFB$=P$AND(PP(16)<>0ORMPG(16)=POZ)THEN2010
2001GOSUB650:IFTEST<>0THEN11261
2002GOSUB600:IFTEST<>0THEN11261
2003IFPOZ<>93ANDPOZ<>91THEN12889
2004IFB$<>"FROG"ANDB$<>P$THEN11148
2010IFB$="FROG"THEN2050
2011IFMPG(16)<>POZTHEN12889
2012?"THE ";P$;" DISAPPEARS IN A BLUE ";RV$;"*POOF*";PV$;" OF SMOKE"
2015?:PRINT"A SMALL GREEN FROG LEAPS FROM THE SMOKE AND QUICKLY"
2020?"HOPS OFF SOMEWHERE OUT OF SIGHT!"
2025?:FR=0:MPG(16)=0:PP(16)=0:NF=99:GOTO11010
2050IFNF<>0THEN11261
2051?"THE ROOM IS FILLED WITH BLUE SMOKE AND WHEN THE SMOKE CLEARS..."
2055?:FR=99:MPG(16)=POZ:GOTO800
2100?"THE ";P$;" REFUSES TO GO BY FORCE!":GOTO11010
2200?"THE ";P$;" ACCEPTS YOUR OFFER AND WILL WALK AT YOUR SIDE."
2210PP(16)=99:MPG(16)=0:GOTO11010
2300PP(16)=0:MPG(16)=POZ:LOOK=0:GOTO800
2400?"THEY ARE TOO HEAVY TO CARRY.":GOTO11010
2500HLP=HLP+1:IFHLP<FNR(HLP)+HLPOR(HLP<2ANDPP(2)<>0)THEN2510
2502?"THE WIZARD SAYS, `QUIT BOTHERING ME!`":IFHLP>2THEN2520
2505GOTO11010
2510IFNL=99ANDHLP<4THEN2515
2512?"NOTHING HAPPENS.":GOTO11010
2515?"THE WIZARD OF `ADVENTURE` WILL LIGHT YOUR WAY FOR 25 MOVES."
2516NL=0:LIGHT=99:OIL=OIL+25
2518MOVES=225:LOOK=0:GOTO10000
2520?"I, THE WIZARD OF `ADVENTURE` HAD GIVEN YOU ENOUGH WARNINGS!!"
2522?"NOW YOU WILL SUFFER THIS FATE........"
2525?:PRINT"THE WIZARD TURNS YOU INTO STONE!!!":PIT=PIT+1
2530?:GOTO14010
3000IFDN<>0ORPOZ=91ORPOZ=93ORPP(16)<>0ORPOZ=87ORB$="MYSELF"THEN3010
3001IFB$="ME"ORB$="KNIGHT"THEN3010
3002GOSUB600:IF TEST<>0THEN11148
3005GOTO12889
3010IFB$="ME"ORB$="MYSELF"THEN3020
3012?"WITH WHAT? YOUR BARE HANDS?":GOTO11010
3020?"ARE YOU SURE YOU WANT ME TO END YOUR MISERABLE LIFE?"
3022INPUT"YES OR NO";A$
3030IFA$="YES"ORA$="NO"THEN3040
3035?:PRINT"ANSWER `YES` OR `NO` DUCK-BRAIN!!!":PRINT:GOTO3020
3040IFMID$(A$,1,1)="Y"THEN14010
3044?"OK!":GOTO11001
4000IFPOZ<>91ANDPOZ<>93THEN12889
4002?"THE FROG HOPS JUST OUT OF REACH."
4010?"IT SEEMS IMPOSSIBLE TO CATCH HIM!":GOTO11010
5000IFDG<>0THEN11246
5001IFPP(11)=0THEN12900
5005?"THE DRAGON DODGES OUT OF THE WAY AND PROMPTLY"
5006?"SITS ON THE AXE WHERE YOU CANNOT GET IT!"
5010NA=99:PP(11)=0:MPG(11)=87:PC=PC-1:GOTO11010
5020?"THE DRAGON IS SITTING ON IT!":GOTO11010
5100IFPP(6)=0THEN?"YOU HAVE NOTHING THAT CAN KILL IT!":GOTO11010
5110?"THE GOLDEN SWORD STRIKES HOME!":NA=0:LOOK=0:DR(87)=9915
5111DG=99:GOTO10000
6000?"THE WATER QUICKLY RUSTS THE ARMOR AND TURNS"
6004?"THE KNIGHT INTO AN IRON STATUE!"
6010?"HE DROPS THE GOLDEN SWORD!":MPG(6)=15:PC=PC-1
6020MPG(5)=59:LOOK=0:BLK=99:GOTO800
10000IFFNR(99)>92ANDPOZ>25THEN700
10010IFMOVES>200ANDDL<>0THEN11000
10020IFMOVES>250ANDNL=0THEN10040
10030GOTO10050
10040?"YOUR LANTERN HAS RUN OUT OF FUEL!":PRINT:PRINT:NL=99
10050IFNL<>0THENLIGHT=0
10052IFPOZ>24ANDPP(2)=0ANDMPG(2)<>POZTHENLIGHT=0
10060IFPOZ<25ORLIGHT<>0THEN11001
10070?"IT IS PITCH DARK! IF YOU PROCEED YOU WILL LIKELY"
10080?"FALL INTO A PIT!":IFFNR(99)>85THEN14000
10090GOTO11010
11000?"YOUR LANTERN IS GETTING VERY DIM! LOOK FOR OIL!":DL=0
11001ONMZ(POZ)GOTO1005,1010,1015,1020,1025,1030,1035,1040,1045,1050
11002ONMZ(POZ)-10GOTO1055,1060,1065,1070,1075,1080,1085,1090,1095,1100
11003ONMZ(POZ)-20GOTO1105,1110,1115,1120,1125,1130,1135,1140,1145,1150
11004ONMZ(POZ)-30GOTO1155,1160,1165,1170,1175,1180
11010CNT=0:IFLOOK<>0THEN11015
11011FORI=1TO20:IFMPG(I)=POZTHENCNT=CNT+1
11012NEXT:IFPOZ=3THEN800
11013IFLIGHT<>0ANDCNT<>0THEN800
11015?:INPUT"COMMAND";A$:IFA$=""THEN11395
11016IFLEN(A$)>0ANDMID$(A$,1,1)=" "THEN11395
11017MOVES=MOVES+1
11018IFPP(16)<>0THENMPG(16)=POZ
11020N=0:E=0:S=0:W=0:U=0:D=0
11025K=LEN(A$)
11030FORI=1TOK
11035IFMID$(A$,I,1)=" "THEN11045
11040NEXT
11045B$="":IFK+1=IORK=ITHEN11055
11050B$=MID$(A$,I+1)
11055A$=MID$(A$,1,I-1)
11100IFLEN(A$)>1THEN11140
11105IFMID$(A$,1,1)="N"THENNP=POZ-5:N=1:GOTO11135
11110IFMID$(A$,1,1)="E"THENNP=POZ+1:E=1:GOTO11135
11115IFMID$(A$,1,1)="S"THENNP=POZ+5:S=1:GOTO11135
11120IFMID$(A$,1,1)="W"THENNP=POZ-1:W=1:GOTO11135
11125IFMID$(A$,1,1)="U"THENNP=POZ-25:U=1:GOTO11135
11130IFMID$(A$,1,1)<>"D"THEN11140
11131NP=POZ+25:D=1
11135DIR=FNO(POZ):IFMID$(A$,1,1)<>"D"THEN11400
11136FORI=1TO2:IFMG(I)<>POZTHENNEXT:GOTO11400
11137?"IT IS LOCKED!":GOTO11010
11140IFA$<>"WALK"ANDA$<>"CRAWL"ANDA$<>"JUMP"ANDA$<>"GO"THEN11150
11141IFLEN(B$)=0THEN?A$;" WHERE";:INPUTB$
11143IFB$<>"NORTH"ANDB$<>"EAST"ANDB$<>"SOUTH"ANDB$<>"WEST"THEN11148
11145A$=B$:GOTO11105
11148?"C`MON, CAN`T YOU BE SERIOUS?":GOTO11010
11150IFA$="NORTH"ORA$="EAST"ORA$="SOUTH"ORA$="WEST"THEN11105
11151IFA$="DOWN"ORA$="UP"THEN11105
11155IFA$="LOOK"THEN?"I`M NOT ALLOWED TO GIVE MORE DETAIL.":GOTO10000
11158IFA$<>"EXIT"ANDA$<>"ENTER"THEN11178
11160IFPOZ=3ORPOZ=8THEN11166
11162?"I`M NOT REALLY SURE WHICH WAY YOU`RE FACING."
11164?"TRY A COMPASS HEADING.":GOTO11010
11166IFPOZ=8THEN11172
11168IFA$<>"EXIT"THEN11176
11170POZ=8:GOTO10000
11172IFA$="ENTER"THENPOZ=3:?"YOU WALK THRU A WOODEN DOOR...":GOTO10000
11176?"YOU DO NOT SEEM TO KNOW WHERE YOU ARE GOING!":GOTO11010
11178IFA$="INV"ORA$="INVENTORY"THEN12700
11180IFA$="GET"THEN12800
11182IFA$<>"LIGHT"ANDA$<>"LITE"THEN11198
11184IFNL<>0THEN11190
11186IFLEN(B$)<>0THEN11192
11188GOTO11395
11190IFNL<>0THEN?"YOUR LANTERN IS OUT OF OIL!":GOTO10000
11192IFB$<>"LANTERN"THEN11148
11194IFPP(2)=0THEN?"YOU DON`T HAVE A LANTERN!":GOTO11010
11196DL=99:LIGHT=99:?"YOUR LANTERN IS LIT!":GOTO10000
11198IFA$<>"QUIT"ANDB$<>"QUIT"THEN11212
11200?:PRINT"DO YOU REALLY WANT TO QUIT";:INPUTA$
11202IFLEN(A$)<4THEN11208
11204?"ANSWER `YES` OR `NO` COWARDLY KNAVE!"
11206INPUT"DO YOU WANT TO QUIT";A$
11208IFMID$(A$,1,1)="Y"THEN30000
11210GOTO10000
11212IFA$="DROP"THEN12900
11214IFA$<>"SAY"THEN11224
11216IFSAY<>0THEN13000
11218IFLEN(B$)<>0THEN11222
11220?A$;" WHAT";:INPUTB$
11222?"OKAY, `";B$;"`!":GOTO11010
11224IFA$<>"OPEN"ANDA$<>"UNLOCK"THEN11230
11225IFPP(1)=0THEN?"YOU HAVE NO KEYS!":GOTO11010
11227FORI=1TO2:IFMG(I)=POZTHEN11229
11228NEXT:?"THERE`S NOTHING HERE TO UNLOCK!":GOTO11010
11229MG(I)=0:?"THE KEYS HAVE ";A$;"ED IT!":GOTO11010
11230IFA$<>"DRINK"THEN11235
11231IFPP(5)=0ORB$<>"WATER"THEN11233
11232?"THANKS, I WAS VERY THIRSTY!":PP(5)=0:PC=PC-1:GOTO11010
11233IFB$<>"WATER"THEN11148
11234?"YOU HAVE TO GET IT FIRST!":GOTO11010
11235IFA$<>"EAT"THEN11240
11236GOSUB650:IFI<3ORPP(I)=0THEN11239
11237PC=PC-1:?"THANKS, DON`T MIND IF I DO!":PRINT
11238PP(I)=0:?"YOUR ";B$;" WAS VERY TASTY!":GOTO11010
11239?"I`M GAME. WOULD YOU CARE TO EXPLAIN HOW?":GOTO11010
11240IFA$<>"THROW"THEN11253
11241IFB$<>"AXE"THEN12900
11242IFPOZ=87THEN5000
11243IFPP(11)<>0THEN11246
11244GOTO12900
11245?"THERE`S NOTHING HERE TO ATTACK!":GOTO11010
11246IFPOZ=15THEN12900
11247IFDN=0THEN11245
11248PP(11)=0:A$="":PC=PC-1:MPG(11)=POZ:B$="YOU MISSED!"
11249B$=B$+" THE FRIGHTENED DWARF RAN AWAY!":IFFNR(99)>70THEN11252
11250A$="YOU KILLED A LITTLE DWARF. HIS BODY"
11251B$="VANISHES IN A "+RV$+"*POOF*"+PV$+" OF GREASY SMOKE!"
11252?A$:PRINTB$:DWF=FNR(3)+1:LOOK=0:PRINT:DN=0:GOTO10000
11253IFA$<>"WAVE"ANDA$<>"SHAKE"THEN11264
11254IFPP(8)=0ORPOZ<>92ORB$<>"WAND"THEN11256
11255RG=99:DR(92)=9907:GOTO11001
11256GOSUB650:IFTEST<>0THEN11261
11257?"YOU DON`T HAVE THAT TO ";A$;"!":GOTO11010
11258DN=0:DWF=FNR(2)+1:?"THE ANGRY LITTLE DWARF SEES THE ANGER"
11259?"IN THE WAY THAT YOU ";A$;" YOUR AXE"
11260?"AND PROMPTLY HEADS FOR THE NEAREST EXIT!":GOTO11010
11261IFDN<>0ANDB$="AXE"THEN11258
11262?"NOTHING HAPPENS!":GOTO11010
11264IFA$<>"PAY"THEN11269
11265IFPP(12)=0THEN?"NO MONEY...NO BOOK!":GOTO11010
11266IFPOZ<>170THEN11395
11267?"THANK YOU SIR, I HOPE YOU ENJOY IT!":PP(14)=99
11268MPG(14)=0:PC=PC+1:FINE=99:GOTO11010
11269IFA$<>"GIVE"THEN11275
11270IFPP(5)<>0ANDPOZ=145ANDB$="WATER"THEN11273
11271IFPP(5)=0THEN12900
11272?"WHO ARE YOU GOING TO GIVE IT TO?":GOTO11010
11273?"THE OLD MAN DRINKS THE WATER AND WALKS OFF INTO THE DARKNESS!"
11274PP(5)=0:PC=PC-1:MZ(145)=28:GOTO10000
11275IFA$<>"WET"THEN11280
11276IFPP(5)=0THEN?"WITH WHAT? SPIT? SWEAT?":GOTO11148
11277IFPOZ<>157THEN11395
11278?"YOU SLIP AND SLIDE INTO......":PRINT:PP(5)=0
11279DR(157)=9908:PC=PC-1:POZ=159:GOTO10000
11280DN=0:IFA$<>"READ"ORPP(14)=0THEN11283
11281IFB$<>"BOOK"THEN11148
11282?"THE BOOK READS, `SAY MAGIC WORD IN THE `NOT SAME` MAZE!`"
11283IFA$="KILL"THEN3000
11284IFA$<>"WITH"THEN11288
11285IFB$<>"AXE"THEN11148
11286A$="THROW":B$="AXE":GOTO11240
11288IFA$=MW$THENB$=MW$:A$="SAY":GOTO11214
11290IFA$="WHY"THEN?"BECAUSE I SAID SO! THAT`S WHY!!!":GOTO11010
11291IFA$="FUCK"ORA$="SHIT"THEN?"WATCH IT!!":GOTO11293
11292IFA$<>"YOU"ANDA$<>"YOUR"ANDB$<>"YOU"THEN11296
11293?"IF YOU DON`T LIKE PLAYING THE GAME,"
11294?"YOU CAN ALWAYS `QUIT`!":GOTO11010
11296IFA$<>"FIND"THEN11300
11297?"I CANNOT TELL YOU WHERE REMOTE THINGS ARE LOCATED!":GOTO11010
11300IFA$="SLAY"ANDB$="DRAGON"THEN5100
11305IFA$<>"YES"THEN11310
11306IFPOZ=15ORPOZ=87ORDN<>0THEN11148
11308?"YOU SEEM UNSURE OF THAT ANSWER!":GOTO11010
11310IFA$<>"LIFT"THEN11320
11312GOSUB600:IFTEST=0ANDPOZ<>99THEN12889
11315IFB$<>"MATRESS"ANDB$<>"MATRESSES"THEN11261
11318MPG(14)=99:LOOK=0:GOTO800
11320IFA$="KISS"THEN2000
11325IFA$="PLEASE"ANDMPG(16)=POZTHEN2200
11330IFA$="WAIT"ANDPP(16)<>0THEN2300
11335IFA$="HELP"THEN2500
11340IFA$<>"LISTEN"THEN11345
11342?"SLOSH...BOIL...DRIP...SPURT...SQUIRT...":GOTO11010
11345IFA$="WHO"ORA$="WHAT"ORA$="WHEN"ORA$="WHERE"ORA$="HOW"THEN11350
11348GOTO11355
11350?"I REFUSE TO ANSWER ON THE GROUNDS THAT"
11352?"IT MIGHT INCRIMINATE ME!":GOTO11010
11355IFMID$(A$,1,1)="N"THEN?"MUST YOU BE SO NEGATIVE?":GOTO11010
11360IFA$<>"CAUSE"ANDA$<>"BECAUSE"THEN11368
11362?"THAT ANSWER STINKS, DONKEY-BRAINED BOTTOM BITER!":GOTO11010
11368REM DO NOT UNDERSTAND
11380IFB$=""THENQ=99
11382B$=A$:GOSUB650:IFTEST=0THEN11395
11384?"WHAT DO YOU WANT TO DO WITH THE ";B$;:INPUTA$:GOTO11180
11390REM DO NOT UNDERSTAND
11395B$="I DON`T UNDERSTAND WHAT YOU MEAN!"
11396IFFNR(99)>50THENB$="WHAT???"
11397IFQ<>0THEN?"I DON`T KNOW THAT WORD!":GOTO11010
11398?B$:GOTO11010
11400IFMZ(POZ)<>6THEN11430
11401IFN=0THEN11405
11402N=0:E=1:NP=POZ+1:GOTO11450
11405IFE=0THEN11410
11406E=0:S=1:NP=POZ+5:GOTO11440
11410IFS=0THEN11420
11412S=0:W=1:NP=POZ-1:GOTO11460
11420IFW=0THEN11430
11422W=0:N=1:NP=POZ-5
11430IFN=0THEN11440
11432IFDIR=1ORDIR=7ORDIR=12ORDIR=14ORDIR=15ORDIR=16THEN11550
11434IFDIR=17ORDIR=19THEN11550
11436GOTO11500
11440IFS=0THEN11450
11442IFDIR=3ORDIR=7ORDIR=11ORDIR=13ORDIR=15ORDIR=16THEN11550
11444IFDIR=18ORDIR=19THEN11550
11446GOTO11500
11450IFE=0THEN11460
11452IFDIR=2ORDIR=8ORDIR=9ORDIR=10ORDIR=11ORDIR=12THEN11550
11454IFDIR=15ORDIR=16ORDIR=17ORDIR=18THEN11550
11456GOTO11500
11460IFW=0THEN11470
11462IFDIR=4ORDIR=8ORDIR=13ORDIR=14ORDIR=15ORDIR=17THEN11550
11464IFDIR=18ORDIR=19THEN11550
11466GOTO11500
11470IFD=0THEN11480
11472IFDIR=5ORDIR=6ORDIR=9THEN11550
11474GOTO11500
11480IFU=0THEN11500
11482IFDIR=0ORDIR=6ORDIR=10THEN11550
11500?:IFPOZ=87THEN11510
11502GOTO11176
11510A$="REFUSES TO LET YOU PASS!":GOTO1142
11550POZ=NP:LOOK=0:GOTO10000
12000N=0:E=0:S=0:W=0:U=0:D=0
12005IFMOVES>50+FNR(50)THENGOSUB200
12010L=INT(DR(POZ)/100)
12020IFL=0THENU=1:GOTO12500
12030IFL=1THENN=1:GOTO12500
12040IFL=2THENE=1:GOTO12500
12050IFL=3THENS=1:GOTO12500
12060IFL=4THENW=1:GOTO12500
12070IFL=5THEND=1:GOTO12500
12080IFL=6THEND=1:U=1:GOTO12500
12090IFL=7THENN=1:S=1:GOTO12500
12100IFL=8THENE=1:W=1:GOTO12500
12110IFL=9THEND=1:W=1:GOTO12500
12120IFL=10THENU=1:E=1:GOTO12500
12130IFL=11THENS=1:E=1:GOTO12500
12140IFL=12THENN=1:E=1:GOTO12500
12150IFL=13THENW=1:S=1:GOTO12500
12160IFL=14THENW=1:N=1:GOTO12500
12170IFL=15THENW=1:N=1:E=1:S=1:GOTO12500
12180IFL=16THENN=1:S=1:E=1:GOTO12500
12190IFL=17THENN=1:W=1:E=1:GOTO12500
12200IFL=18THENS=1:W=1:E=1:GOTO12500
12210IFL=19THENS=1:W=1:N=1:GOTO12500
12220REM LOOK AROUND
12500IFN<>0THENLT=POZ-5:N=0:?"TO THE NORTH ";:GOTO12570
12510IFE<>0THENLT=POZ+1:E=0:?"ALONG THE EAST ";:GOTO12570
12520IFS<>0THENLT=POZ+5:S=0:?"LOOKING SOUTH ";:GOTO12570
12530IFW<>0THENLT=POZ-1:W=0:?"APPEARING WESTERLY ";:GOTO12570
12540IFU<>0THENLT=POZ-25:U=0:GOTO12570
12550IFD<>0THENLT=POZ+25:D=0:GOTO12570
12560GOTO11010
12570ONMZ(LT)GOTO1008,1013,1018,1023,1028,1033,1038,1043
12580GOTO11010
12700?:PRINT"YOU ARE HOLDING THE FOLLOWING ITEMS:":PRINT
12701FORI=1TO16:IFI=16ANDPP(16)<>0THEN12705
12702IFPP(I)<>0THEN?MID$(THG$,THG(I),THG(I+1)-1-THG(I))
12705NEXT
12795FORI=1TO20:IFPP(I)<>0THEN11010
12798NEXT:?"NOTHING.":GOTO11010
12800CNT=0:FORI=1TO20:IFMPG(I)=POZTHENCNT=CNT+1
12802NEXT
12810IFPC<6THEN12815
12811?"YOU CAN`T CARRY ANYTHING MORE."
12812?"YOU`LL HAVE TO DROP SOMETHING FIRST!":GOTO11010
12815IFLEN(B$)=0THEN?A$;:INPUT" WHAT";B$
12820GOSUB600
12825IFTEST<>0THEN12842
12826IFB$="WATER"ANDPOZ=59THEN12842
12827IFB$="DRAGON"THEN11148
12828IFB$="FROG"THEN4000
12830IFB$="MATRESS"ORB$="MATRESSES"THEN2400
12832IFMPG(16)=POZTHEN2100
12840GOTO12889
12842IFB$<>"WATER"ORMPG(5)<>POZTHEN12850
12843IFPP(5)<>0THEN12847
12844IFPP(4)<>0THENPP(5)=99:GOTO12890
12846?"YOU HAVE NOTHING TO CARRY THE WATER IN!":GOTO11010
12847?"YOU CAN`T CARRY ANY MORE WATER!":GOTO11010
12850IFB$<>"OIL"ORMPG(12)<>POZTHEN12874
12852MPG(12)=0:?"YOUR LANTERN IS FULL AGAIN!"
12854OIL=MOVES:DL=0:MOVES=1:GOTO11010
12874IFB$="BOOK"ANDFINE=0ANDPOZ=159THEN1231
12875IFB$<>"BOOK"ORMPG(14)<>POZTHEN12878
12876PP(14)=99:MPG(14)=0:GOTO12890
12878IFB$<>"TREASURE"ORMPG(7)<>POZTHEN12885
12879IFMG(2)=0THENPP(7)=99:MPG(7)=0:GOTO12890
12880?"IT`S LOCKED!":GOTO11010
12885IFB$<>"AXE"THEN12888
12886IFNA<>0THEN5020
12888PP(I)=99:MPG(I)=0:GOTO12890
12889?"I SEE NO ";B$;" HERE!":GOTO11010
12890?"OK!":PC=PC+1:GOTO11010
12900IFPC<>0THEN12910
12901?"YOU HAVE NOTHING TO ";A$;"!":GOTO11010
12905IFLEN(B$)=0THEN?A$;" WHAT?":GOTO11010
12910GOSUB650:IFTEST<>0THEN12920
12911IFB$="PRINCESS"THEN2010
12919?"YOU ARE NOT CARRYING IT!":GOTO11010
12920IFB$="LANTERN"THENDL=0
12922IFB$<>"BOTTLE"ORPP(4)=0THEN12927
12923PP(4)=0:MPG(4)=POZ:IFPP(5)=0THEN12995
12924PP(5)=0
12925?"THE BOTTLE TIPPED OVER AND THE WATER SPILLED OUT!"
12926PC=PC-1:GOTO12995
12927IFB$<>"STATUE"ORPP(6)=0THEN12933
12928IFMPG(7)=POZTHEN12930
12929?"THE STATUE FALLS AND BREAKS WORTHLESSLY!":GOTO11932
12930PP(6)=0:MPG(6)=POZ:IFBREAK<>0THEN11010
12931?"THE STATUE LANDS SAFELY ON THE PERSIAN RUG!":GOTO12995
12932PP(6)=0:MPG(6)=0:GOTO12995
12933PP(I)=0:MPG(I)=POZ
12935IFB$="WATER"ANDA$="THROW"ANDPOZ=15ANDBLK=0THEN6000
12995?"OK!":PC=PC-1:GOTO11010
13000IFB$<>MW$THEN11222
13010GOTO13030
13020?"A SECRET PASSAGE OPENS TO THE WEST!":DR(199)=9914:GOTO11010
13030IFPOZ=3THENPOZ=41:GOTO13037
13035IFPOZ<>41THEN13040
13036POZ=3
13037LOOK=0
13040IFPOZ=3ORPOZ=41THEN10000
13042?"NOTHING HAPPENS!":GOTO11010
14000?:PRINT"YOU FELL INTO A PIT AND ARE"
14002?"A MASS OF TWISTED FLESH AND BONES!":PRINT
14010PIT=PIT+1:IFPIT>2THEN14030
14020?:PRINT"I MIGHT BE ABLE TO REINCARNATE YOU!"
14022?"WOULD YOU LIKE ME TO TRY";:GOTO14050
14030?"YOU`VE REALLY DONE IT THIS TIME! I`M ALL OUT OF FAIRY DUST!"
14035?:PRINT"YOU DON`T REALLY EXPECT ME TO DO A"
14036?"DECENT REINCARNATION WITHOUT IT DO YOU";
14037KILL=0:DWF=0
14050IFPIT>3THEN14070
14051INPUTA$:IFMID$(A$,1,1)="N"THEN30000
14052?"READY...ONE...TWO...THREE...";RV$;"*POOF*";PV$
14055?:PRINT"YOU EMERGE FROM A CLOUD OF FAIRY DUST TO FIND YOURSELF..."
14060LOOK=0:FORI=1TO20:IFI=2THEN14063
14061IFPP(I)<>0THENMPG(I)=POZ
14063NEXT:FORI=1TO20:PP(I)=0:NEXT:POZ=3:MPG(2)=3:PC=0:GOTO11001
14070INPUTA$:IFMID$(A$,1,1)="N"THEN30000
14080?:PRINT"OK! IF YOU`RE SO SMART, DO IT YOURSELF!!":PRINT
30000PTS=0:FORI=1TO5:IFMPG(I)=3THENPTS=PTS+10
30004NEXT
30010FORI=6TO10:IFMPG(I)=3THENPTS=PTS+20
30012NEXT
30015FORI=11TO15:IFMPG(I)=3THENPTS=PTS+30
30016NEXT
30017IFMPG(16)=3THENPTS=PTS+50
30020IFSAY<>0THENPTS=PTS+50
30030IFOIL>0THENMOVES=MOVES+OIL
30035PTS=PTS-INT(MOVES/10)
30040?:PRINT"YOU SCORED ";PTS;" POINTS OUT OF A"
30042?"POSSIBLE 350, USING ";MOVES;" TURNS!"
30045A$=" RANK BEGINNER":IFPTS>50THENA$=" NOVICE"
30046IFPTS>100THENA$="N EXPERIENCED"
30047IFPTS>150THENA$=" SEASONED"
30050IFPTS>200THENA$=" JR. MASTER"
30051IFPTS>250THENA$=" SR. MASTER"
30052IFPTS>300THENA$=" JR. WIZARD"
30053IFPTS=>350THENA$=" "+RV$+"WIZARD"+PV$
30055?"YOUR SCORE QUALIFIES YOU FOR A";A$;" ADVENTURER!"
30060FORI=50TO350STEP50:IFPTS-I>0THENNEXT
30065?"TO ACHIEVE THE NEXT HIGHER RATING"
30070?"YOU NEED ";I-PTS;" MORE POINTS!":END
STOP
